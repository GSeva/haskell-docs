\section{Mónadas} % (fold)
\label{sec:m_nadas}

Las \textbf{mónadas} en Haskell se pueden pensar como descripciones
\textit{componibles} de computaciones. Presentan la posibilidad de separar la
combinación de computaciones de su ejecución y permiten acarrear datos extra
implícitamente en adición al resultado de la computación, que
\textit{se producirá} cuando la mónada sea corrida. De esta manera permiten
suplementar las funcionalidades \textit{puras} con I/O, estado, indeterminismo,
etc.

En terminos del lenguaje una mónada es un tipo parametrizado que es instancia
de la clase \textit{Monad}. Su definición es la siguiente:

\begin{lstlisting}
class Monad m where
    return :: a -> m a
    (>>=) :: m a -> (a -> m b) -> m b
    (>>) :: m a -> m b -> m b
\end{lstlisting}

Podemos ver la mónada como un contenedor para un valor \textbf{a}. La función
\lstinline$return$ se ocupa de poner ese valor adentro de la mónada. Entonces
la función \lstinline$(>>=)$, tambien conocida como \textit{bind}, aplica la
función que se le pasa por parámetro al contenido de la mónada obteniendo como
resultado otra mónada (obviamente la función pasada tiene que tener el tipo
adecuado). Se puede ver como funciona en el siguiente ejemplo:

\begin{lstlisting}
putStrLn "Como te llamas?"
>>= (\_ -> getLine)
>>= (\name -> putStrLn ("Hola, " ++ name ++ "!"))
\end{lstlisting}

El operador \lstinline$(>>=)$ se ocupa de tomar el valor del lado izquierdo
y combinarlo con la función del lado derecho para producir un valor nuevo. El
ejemplo de arriba se puede reescribir con la notación \lstinline$do$, que es un
azucar sintáctico alrededor del operador \textit{bind}:

\begin{lstlisting}
do
   putStrLn "Como te llamas?"
   name <- getLine
   putStrLn ("Hola, " ++ name ++ "!")
\end{lstlisting}

Ese código puede parecer de un lenguaje imperativo, y de hecho lo es: otra
forma de ver las mónadas es pensar que son la abstracción necesaria para
suplementar las funcionalidades que no cuadran adentro del paradigma funcional.

La implementación mas sencilla del \lstinline$(>>=)$. toma el valor del lado
izquierdo, le aplica la función y devuelve el resultado, sin embargo se vuelve
realmente útil cuando esa implementación hace algo extra.

\subsection{Las Leyes de las Mónadas} % (fold)
\label{sub:las_leyes_de_las_m_nadas}

Las mónadas por convención deben cumplir las siguientes leyes:

\begin{lstlisting}
  -- Identidad por la izquierda
  return x >>= f = f x

  -- Identidad por la derecha
  m >>= return = m

  -- Asociatividad
  (m >>= f) >>= g = m >>= (x -> f x >>= g)
\end{lstlisting}

Para poder entenderlo mejor y usarlo de una forma más sencilla, el módulo
Control.Monad define el operador de composición de mónadas:

\begin{lstlisting}
(>=>) :: Monad m => (a -> m b) -> (b -> m c) -> a -> m c
(m >=> n) x = do
                  y <- m x
                  n y
\end{lstlisting}

Con el uso de ese operador las tres reglas pueden escribirse de la siguiente
forma:

\begin{lstlisting}
  -- Identidad por la izquierda
  return >=> f = f

  -- Identidad por la derecha
  f >=> return = f

  -- Asociatividad
  (f >=> g) >=> h = f >=> (g >=> h)
\end{lstlisting}

% subsection las_leyes_de_las_m_nadas (end)

\subsection{Mónadas comunes} % (fold)
\label{sub:m_nadas_comunes}

La siguiente tabla lista las mónadas más comunes usadas en Haskell, denotando
el problema que tratan de solucionar en términos imperativos.
\linebreak
\begin{tabular}{ | p {3cm} | p {5cm} |}
  \hline
  Mónada & Semántica imperativa \\
  \hline
  \hline
  Maybe & Excepción anónima \\
  \hline
  Error & Excepción con descripción \\
  \hline
  State & Estado global \\
  \hline
  IO & Entrada y Salida \\
  \hline
  [] (list) & Indeterminismo \\
  \hline
  Reader & Entorno \\
  \hline
  Writer & Logger \\
  \hline
\end{tabular}

En las siguientes secciones se explicarán algunas de las mónadas que aparecen
en esta tabla.
% subsection m_nadas_comunes (end)


\subsection{Manejo de errores} % (fold)
\label{sub:manejo_de_errores}


\subsubsection{Mónada Maybe} % (fold)
\label{ssub:m_nada_maybe}

Es una de las mónadas más utilizadas y es muy sencilla a la vez. La definición
de esta Mónada es la siguiente:

\begin{lstlisting}
data Maybe a = Nothing | Just a

instance Monad Maybe where
    return         = Just
    fail           = Nothing
    Nothing  >>= f = Nothing
    (Just x) >>= f = f x
\end{lstlisting}

La mónada Maybe incorpora la posibilidad de encadenar computaciones que pueden
devolver Nothing como resultado, en cuyo caso la cadena terminaría antes. Por ejemplo:

\begin{lstlisting}
maybeHalf :: Int -> Maybe Int
maybeHalf a
         | even a = Just (div a 2)
         | otherwise = Nothing
\end{lstlisting}

La función que acabamos de definir devolverá la mitad de un número, si es par, y \textbf{Nothing} si es impar. La podemos usar de la siguiente manera:

\begin{lstlisting}
ghc> Just 10 >>= maybeHalf
Just 5
ghc> Just 10 >>= maybeHalf >>= maybeHalf
Nothing
ghc> Just 10 >>= maybeHalf >>= maybeHalf >>= maybeHalf
Nothing
\end{lstlisting}

Sin el uso de la mónada Maybe nos veriamos obligados a anidar if y else para las llamadas consecutivas.

\subsubsection{Either} % (fold)
\label{ssub:either}

Otra mónada muy usada es \textit{Either}. Su tipo está definido de la
siguiente manera:

\begin{lstlisting}
data Either a b = Left a | Right b
\end{lstlisting}

Su propósito es muy parecido al de Myabe, Left es considerado un error
mientras que Right - un valor normal. La diferencia es que Left permite
guardar un valor (a diferencia del Nothing en el caso de Maybe).

Podemos escribir una función que divida un número por una lista de enteros uno por uno segura utilizando \textbf{Either}:

\begin{lstlisting}
divBy :: Integral a => a -> [a] -> Either String [a]
divBy _ [] = Right []
divBy _ (0:_) = Left "divBy: division by 0"
divBy numerator (denom:xs) =
    case divBy numerator xs of
      Left x -> Left x
      Right results -> Right ((numerator `div` denom) : results)
\end{lstlisting}

Y la podemos utilizar de la siguiente forma:

\begin{lstlisting}
ghci> divBy 50 [1,2,5,8,10]
Right [50,25,10,6,5]
ghci> divBy 50 [1,2,0,8,10]
Left "divBy: division by 0"
\end{lstlisting}

% subsubsection _ither (end)


\subsubsection{Mónada Error} % (fold)
\label{ssub:m_nada_error}

La mónada Error es la forma que tiene Haskell de simular las
\textit{excepciones}. Se logra de la siguiente forma: se encadenan
computaciones que pueden lanzar excepción derivando la ejecución a la
instancia que puede manejarla. Esta es la definición de \textbf{MonadError}:

\begin{lstlisting}
class Monad m => MonadError e m | m -> e where
    throwError :: e -> m a
    catchError :: m a -> (e -> m a) -> m a
\end{lstlisting}

Un ejemplo de uso es:

\begin{lstlisting}
example :: (Error e, MonadError e m) => m String
example = throwError (strMsg "Esto es un error")
    `catchError` const (return "Atrapo el error")

example >>= putStrLn        -- Imprime "Atrapo el error"
\end{lstlisting}

Note que el tipo MonadError lleva 2 parametros, \textbf{e}, el tipo de error, y \textbf{m}, constructor que representa una mónada. Mas adelante en la definición se ve que el \textbf{throwError} y \textbf{catchError} son métodos de la clase MonadError, siendo los equivalentes a las estructuras familiares de otros lenguajes. Eso tiene la ventaja de poder definir su significado para cada mónada particular.

% subsection m_nada_error (end)

% subsection manejo_de_errores (end)

\subsection{Mónada List} % (fold)
\label{sub:m_nada_list}


Resulta que la lista es una mónada tambien! Las listas se utilizan para modelar
las computaciones no determinísticas que pueden devolver un número de
resultados arbitrario. Se define de la siguiente manera:

\begin{lstlisting}
instance Monad [] where
    m >>= f  = concat (map f m)
    return x = [x]
\end{lstlisting}

El return es fácil de entender: simplemente devuelve una lista de un elemento.
El operador \textit{bind} tambien se entiende si consideramos que tiene que
cumplir con el tipo \lstinline$[a] -> (a -> [b]) -> [b]$ (por su defenición
más general). La función \textbf{f} aplica a cada elemento y, dado su tipo,
retorna una lista - por eso es necesario el \lstinline$concat$ al final, para
a la salida obtener una lista.

¿Por qué las listas son mónadas? Se explica con el siguiente ejemplo de
notación monádica de una lista:

\begin{lstlisting}
foo = do
  x <- [1 .. 10]
  y <- [2, 3, 5, 7]
  return (x * y)
\end{lstlisting}

\textbf{foo} es un múltiplo de x e y, con x siendo un número no determinístico
entre 1 y 10, e y siendo 2, 3, 5 o 7. Este ejemplo es la notación larga y
monádica de una comprensión de lista, podría ser escrita así:

\begin{lstlisting}
foo = [x * y | x <- [1 .. 10], y <- [2, 3, 5, 7]]
\end{lstlisting}
% subsection m_nada_list (end)


\subsection{Mónada IO} % (fold)

Dado que Haskell es un lenguaje funcional y perezoso, no podemos expresar las
efectos reales de las operaciones de entrada y salida con funciones puras. De
hecho, estas operaciones no se pueden ejecutar perezosamente, ya que esto
haria que los efectos reales sean impredecibles. La mónada \textbf{IO} es el
medio para representar las acciones de entrada/salida como valores de Haskell,
para que el programador pueda manipularlos con funciones puras.


\label{sub:m_nada_io}

% subsection m_nada_io (end)


\subsection{Mónada State} % (fold)
\label{sub:m_nada_state}

La mónada \textit{State} permite acarrear estado a lo largo de una ejecución.
Con ella se puede hacer lo siguiente: dado un valor de estado, se produce un
resultado y un nuevo valor de estado. Esta es su definición:

\begin{lstlisting}
newtype State s a = State { runState :: (s -> (a,s)) }

instance Monad (State s) where
  return x = State $ \s -> (x,s)
  (State h) >>= f = State $ \s -> let (a, newState) = h s
                                      (State g) = f a
                                  in  g newState
\end{lstlisting}

Como se ve en la declaración de tipo, State es solo una abstracción
de una función que toma un estado, devuelve un valor intermedio y un nuevo
estado.

Un ejemplo de uso de State es una pila. La función \textbf{push}
agrega un elemento al tope de la pila y \textbf{pop} saca uno. Sin State
tendriamos que arrastrar la pila como argumento a estas funciones, lo cual
no es lo más cómodo de usar. Con State la podemos definir de la siguiente
forma:

\begin{lstlisting}
type Stack = [Int]

pop :: State Stack Int
pop = State $ \(x:xs) -> (x,xs)

push :: Int -> State Stack ()
push a = State $ \xs -> ((),a:xs)
\end{lstlisting}

Un ejemplo de uso de la pila sería:

\begin{lstlisting}
stackManip = do
    push 3
    a <- pop
    pop
\end{lstlisting}

La implementación de State en Control.Monad también es instancia de la clase
MonadState que define 2 funciones muy útiles:

\begin{lstlisting}
put newState = State $ \_ -> ((), newState)
get = State $ \st -> (st, st)
\end{lstlisting}

Las cuales nos permiten manejar el estado de una manera más sencilla.

% subsection m_nada_state (end)

\subsection{Mónada Reader} % (fold)
\label{sub:m_nada_reader}

Algunos problemas de programación requieren computaciones en un cierto entorno (como un set de variables), pero no requieren la generalidad de la mónada \textbf{State}. La mónada \textbf{Writer} permite aportarle a una computación un entorno. Esta es su definición de tipo y su instancia de mónada:

\begin{lstlisting}
newtype Reader r a = Reader {  runReader :: r -> a }

instance Monad (Reader e) where
    return a         = Reader $ \e -> a
    (Reader r) >>= f = Reader $ \e -> runReader (f (r e)) e
\end{lstlisting}

En el siguiente ejemplo se puede ver facilmente su utilidad:

\begin{lstlisting}
import Control.Monad.Reader

greeter :: Reader String String
greeter = do
    name <- ask
    return ("Hola, " ++ name ++ "!")
\end{lstlisting}

\begin{lstlisting}
ghci> runReader greeter "Jorge"
"Hola, Jorge!"
\end{lstlisting}

Usamos la función \textbf{ask} que esta definida en la clase \textbf{MonadReader}. Esta es su definición y el como Reader implementa esa clase:

\begin{lstlisting}
class MonadReader e m | m -> e where
    ask   :: m e
    local :: (e -> e) -> m a -> m a

instance MonadReader (Reader e) where
  ask       = Reader id
  local f c = Reader $ \e -> runReader c (f e)
\end{lstlisting}

Esta clase provee de funciones para que el uso de \textbf{Reader} sea más cómodo. \textbf{ask} recupera el entorno y \textbf{local} ejecuta la computación en el entorno modificado.

% subsection m_nada_reader (end)

\subsection{Mónada Writer} % (fold)
\label{sub:m_nada_writer}

La mónada \textbf{Writer} sirve para producir un stream de datos adicional a los valores computados.
Es útil para generar un log sobre una ejecución.
Esta es la definición del tipo \textbf{Writer}. Como con los ejemplos anteriores, nos permite redefinir su comportamiento:

\begin{lstlisting}
newtype Writer w a = Writer { runWriter :: (a, w) }
\end{lstlisting}

A continuación se puede ver un ejemplo sencillo de uso del Writer:

\begin{lstlisting}
import Control.Monad.Writer

doubleWithLog :: Int -> Writer String Int
doubleWithLog x = do
        tell ("Acabo de duplicar " ++ (show x) ++ "!")
        return (x * 2)
\end{lstlisting}

Si ejecutamos esta función, obtendremos lo siguiente:

\begin{lstlisting}
ghci> runWriter $ doubleWithLog 8
(16, "Acabo de duplicar 8!")
\end{lstlisting}

% subsection m_nada_writer (end)


% section m_nadas (end)
